data "aws_vpc" "default" {
    default = true 
}

resource "aws_security_group" "sg" {
    name = "my-security-group"
    description = "my-security-group"
    vpc_id = data.aws_vpc.default.id 

    ingress {
        from_port = var.ingress_ssh
        to_port = var.ingress_ssh
        protocol = "tcp"
        cidr_blocks = ["0.0.0.0/0"]
    }

    ingress {
        from_port = var.ingress_http
        to_port = var.ingress_http
        protocol = "tcp"
        cidr_blocks = ["0.0.0.0/0"]
    }

    egress {
        from_port = 0
        to_port = 0
        protocol = "-1"
        cidr_blocks = ["0.0.0.0/0"]
    }

    tags = {
        Name = "my-security-group"
    }
}

resource "aws_lb_target_group" "tg" {
    name = "web-tg"
    port = var.tg_port
    protocol = "HTTP"
    vpc_id = data.aws_vpc.default.id 
    health_check {
      path = "/"
    }
}

resource "aws_lb" "lb" {
    name = "my-alb"
    internal = false 
    load_balancer_type = var.load_balancer_type
    security_groups = [aws_security_group.sg.id]
    subnets = [
        "subnet-0540409a5589cce19",
        "subnet-0404cbc0f5f6977f7"
    ]
}

resource "aws_lb_listener" "lb_listener" {
    load_balancer_arn = aws_lb.lb.arn 
    port = var.tg_port
    protocol = "HTTP"
    default_action {
        type = "forward"
        target_group_arn = aws_lb_target_group.tg.arn 
    }
}

resource "aws_launch_template" "lt" {
    name_prefix = "web-launch-template"
    image_id = var.ami 
    key_name = var.key_name
    vpc_security_group_ids = [aws_security_group.sg.id]
    instance_type = var.instance_type
    user_data = filebase64("${path.module}/user-data.sh")
}

resource "aws_autoscaling_group" "asg" {
    name = "my-asg"
    desired_capacity = var.desired_capacity
    max_size = var.max_size
    min_size = var.min_size

    launch_template {
      id = aws_launch_template.lt.id 
      version = var.lt_version
    }

    target_group_arns = [aws_lb_target_group.tg.arn]

    health_check_type = "ELB"

    vpc_zone_identifier = [
        "subnet-0540409a5589cce19",
        "subnet-0404cbc0f5f6977f7"
    ]
}